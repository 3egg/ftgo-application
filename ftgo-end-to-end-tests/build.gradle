plugins {
  id 'org.hidetake.swagger.generator' version '2.18.1'
}

apply plugin: 'docker-compose'
apply plugin: FtgoApiDependencyResolverPlugin

dependencies {
  swaggerCodegen 'org.openapitools:openapi-generator-cli:3.3.4'     // or OpenAPI Generator
}

swaggerSources {
  consumerService {
    inputFile = file("${ftgoApiSpecsDir}/ftgo-consumer-service-swagger.json")
    code {
      language = 'java'
      configFile = file('swagger-codegen-config/consumer-service.json')
      components = ['models']
      // Supports additionalProperties: https://github.com/swagger-api/swagger-codegen#to-generate-a-sample-client-library
    }
  }
}

swaggerSources.consumerService.code.dependsOn ftgoResolveAPIDependencies

compileJava.dependsOn swaggerSources.consumerService.code

sourceSets.main.java.srcDir "${swaggerSources.consumerService.code.outputDir}/src/main/java"

// This is unnecessary

sourceSets.main.resources.srcDir "${swaggerSources.consumerService.code.outputDir}/src/main/resources"

dependencies {
    ftgoApiSpecification project(":ftgo-consumer-service-api-spec")

    compile project(":ftgo-accounting-service-api")
    compile project(":ftgo-consumer-service-api")
    compile project(":ftgo-kitchen-service-api")
    compile project(":ftgo-restaurant-service-api")
    compile project(":ftgo-order-service-api")
    compile project(":ftgo-delivery-service-api")

    compile "io.eventuate.util:eventuate-util-test:$eventuateUtilVersion"

    compile 'io.swagger:swagger-annotations:1.5.24'

    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testCompile "com.jayway.restassured:rest-assured:$restAssuredVersion"
    testCompile "com.jayway.jsonpath:json-path:2.3.0"


}

dockerCompose {

}

test.dependsOn(composeUp)
